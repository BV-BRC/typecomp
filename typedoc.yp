
%start start

%{
    use Devel::StackTrace;
    use KBT;
    use Data::Dumper;
    use File::Spec;

our %builtin_types = ('int' => KBT::Scalar->new(scalar_type => 'int'),
		      'string' => KBT::Scalar->new(scalar_type => 'string'),
		      'float' => KBT::Scalar->new(scalar_type => 'float'),
    );


%}

%%


start:		module_list
	;

module_list: 	{ [] }
	|      	module_list module { [ @{$_[1]}, $_[2] ] }
	;

module:		module_opts MODULE { $_[0]->get_comment() }  IDENT '{' module_components '}' ';' {
    KBT::DefineModule->new(options => $_[1],
			   module_name => $_[4],
			   module_components => $_[6],
		           comment => $_[3]);
    }
	;

module_opts: { [] }
	|	module_opts module_opt { [ @{$_[1]}, $_[2] ] }
	;

module_opt:	AUTHENTICATED 
        ;


module_components: { [] }
	|	module_components module_component_with_doc { [ @{$_[1]}, $_[2] ] }
	;

module_component_with_doc:
		module_component
	|	DOC_COMMENT module_component { $_[2]->comment($_[1]); $_[2] }
	;

module_component:
		typedef
	|	funcdef 
	|	'use' 'module' ident ';'
	;

typedef:	TYPEDEF { $_[0]->get_comment() } type IDENT  ';' { $_[0]->define_type($_[3], $_[4], $_[2]); }
	;

funcdef:	FUNCDEF { $_[0]->get_comment() } IDENT '(' funcdef_params ')' RETURNS '(' funcdef_params ')' ';'
			{ KBT::Funcdef->new(return_type => $_[9], name => $_[3], parameters => $_[5],
			      comment => $_[2]); }
	|	FUNCDEF { $_[0]->get_comment() } type IDENT '(' funcdef_params ')' ';'
			{ KBT::Funcdef->new(return_type => [$_[3]], name => $_[4], parameters => $_[6],
			      comment => $_[2]); }
	;

funcdef_params: { [] }		# empty
	|	funcdef_param { [ $_[1] ] }
	|	funcdef_params ',' funcdef_param { [ @{$_[1]}, $_[3] ] }
	;

funcdef_param: type IDENT 		{ { type => $_[1], name => $_[2] } }
	|	type 			{ { type => $_[1] } }
	;


type:		TYPENAME
	|	mapping
	|	structure
	|	list
	|	tuple
	;

mapping:	MAPPING '<' tuple_type ',' tuple_type '>' { KBT::Mapping->new(key_type => $_[3], value_type=> $_[5]); }
	;

structure:	STRUCTURE '{' struct_items '}' { KBT::Struct->new(items => $_[3]); }
	;

struct_items:	struct_item { [$_[1]] }
	|	struct_items struct_item { [ @{$_[1]}, $_[2] ] }
	;

struct_item:	type IDENT ';' { KBT::StructItem->new(item_type => $_[1], name => $_[2], nullable => 0); }
	|	type IDENT NULLABLE ';' { KBT::StructItem->new(item_type => $_[1], name => $_[2], nullable => 1); }
	;

list:		LIST '<' type '>' { KBT::List->new(element_type => $_[3]); }
	;

tuple:		TUPLE '<' tuple_types '>' { KBT::Tuple->new(element_types => $_[3]); }
	;

tuple_types:	tuple_type { [ $_[1] ] }
	|	tuple_types ',' tuple_type { [ @{$_[1]}, $_[3] ] }
	;

tuple_type:	type
	|	type IDENT { $_[1] }
	;

%% 

sub define_type
{
    my($self, $old_type, $new_type, $comment) = @_;
    my $def = KBT::Typedef->new(name => $new_type, alias_type => $old_type, comment => $comment);
    push(@{$self->YYData->{type_list}}, $def);
    $self->YYData->{type_table}->{$new_type} = $def;
    return $def;
}

sub types
{
    my($self) = @_;
    return $self->YYData->{type_list};
}

sub lookup_type
{
    my($self, $name) = @_;
    return $self->YYData->{type_table}->{$name};
}


sub parse
{
    my($self, $data) = @_;

    #
    # Initialize type table to just the builtins.
    #
    $self->YYData->{type_table} = { %builtin_types };

    $self->YYData->{INPUT} = $data;

    my $res = $self->YYParse(yylex => \&Lexer, yyerror => \&Error);

    return $res;
}


sub Error {
    my($parser) = @_;
    
    my $data = $parser->YYData;
    my $bufptr = \$data->{INPUT};

    my $ctx = substr($$bufptr, 0, 100);

    if ($data->{ERRMSG})
    {
        print $data->{ERRMSG};
	print "$ctx\n";
        delete $data->{ERRMSG};
        return;
    }
    else
    {
	print "Syntax error.\n";
	print "$ctx\n";
    }
}

sub Lexer {
    my($parser)=shift;

    my $data = $parser->YYData;
    my $bufptr = \$data->{INPUT};

    for ($$bufptr)
    {
	while ($_ ne '')
	{
	    # print "Top: '$_'\n";
	    s/^[ \t\n]+//;

	    
	    if ($_ eq '')
	    {
		return ('', undef);
	    }
	    elsif (s/^(funcdef|typedef|module|list|mapping|structure|nullable|returns|authenticated|tuple)\b//)
	    {
		return (uc($1), $1);
	    }
	    elsif (s/^([A-Za-z][A-Za-z0-9_]*)//)
	    {
		#print "Check builtin $1 " . Dumper($data->{type_table});
		
		if (my $type = $data->{type_table}->{$1})
		{
		    return('TYPENAME', $type);
		}
		else
		{
		    return('IDENT',$1);
		}
	    }
	    elsif (s,^/\*(.*?)\*/,,s)
	    {
		my $com = $1;
		if ($com =~ /^\*/)
		{
		    #
		    # It was a /** comment which is a doc-block. Return that as a token.
		    #
		    return('DOC_COMMENT', $com);
		}

		my @lines = split(/\n/, $com);
		$lines[0] =~ s/^\s*//;
		my @new = ($lines[0]);
		shift @lines;
		if (@lines)
		{
		    my $l = $lines[0];
		    $l =~ s/\t/        /g;
		    my($init_ws) = $l =~ /^(\s+)/;
		    my $x = length($init_ws);
		    # print "x=$x '$lines[0]'\n";
		    for my $l (@lines)
		    {
			$l =~ s/\t/        /g;
			$l =~ s/^\s{$x}//;
			push(@new, $l);
		    }
		}
		#$parser->{cur_comment} = $com;
		$parser->{cur_comment} = join("\n", @new);
		
		# Else just elide.
	    }
	    elsif (s/^(.)//s)
	    {
		return($1,$1);
	    }
	}
    }
}

#
# Return the current comment if there is one. Always
# clear the current comment.
#
sub get_comment
{
    my($self) = @_;
    my $ret = $self->{cur_comment};
    $self->{cur_comment} = "";
    $ret =~ s/^\s+//;
    $ret =~ s/\s+$//;
    return $ret;
}
    
